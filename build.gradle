import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'application'
}

group = 'ru.rdc.mosmedii'
version = '1.1'

java {
    sourceCompatibility = '17'
}

//Чтобы gradle обрабатывал ресурсы приложения
processResources {
    filesMatching('application.properties') {
        expand(project.properties)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

def javaFXPlatform = getJavaFXPlatform()
def javaFXVersion = "20"

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    //implementation 'org.apache.httpcomponents:httpclient:4.5.14'

    implementation 'org.postgresql:postgresql:42.6.0'
    //implementation 'com.google.code.gson:gson:2.9.1'
    //implementation 'org.json:json:20240303'

    implementation "org.openjfx:javafx-controls:${javaFXVersion}:${javaFXPlatform}"
    implementation "org.openjfx:javafx-graphics:${javaFXVersion}:${javaFXPlatform}"
    implementation "org.openjfx:javafx-fxml:${javaFXVersion}:${javaFXPlatform}"
    implementation "org.openjfx:javafx-base:${javaFXVersion}:${javaFXPlatform}"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

//Определяем платформу, т.е. ОС
private static String getJavaFXPlatform() {
    //def currentOS = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem;
    def currentOS = DefaultNativePlatform.currentOperatingSystem;
    if (currentOS.isWindows()) {
        return 'win'
    } else if (currentOS.isLinux()) {
        return 'linux'
    } else if (currentOS.isMacOsX()) {
        return 'mac'
    }
    return null
}
